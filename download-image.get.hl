
/*
 * Returns the specified image to caller.
 * 
 * NOTICE! This file should probably be consumed as a Macro, and not as a template, since this
 * will correctly decorate your file with the correct properties.
 */
.description:Returns the specified image to caller
.type:image-download
.arguments
   image:string

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/image

// Verifying user has access to invoke endpoint.
auth.ticket.verify:root, admin, guest

// Sanity checking invocation.
if
   not
      strings.starts-with:x:@.arguments/*/image
         .:/etc/tickets/
   .lambda
      throw:Access denied
         public:true
         status:400

// Opening up a read stream for file.
io.stream.open-file:x:@.arguments/*/image

// Making sure we get the HTTP headers correctly applied.
response.headers.set
   Content-Type:application/octet-stream
   Cache-Control:public, max-age=6000

// Returning image to caller.
return:x:@io.stream.open-file
