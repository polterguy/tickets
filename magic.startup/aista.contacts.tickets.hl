
/*
 * Plugin slot returning tickets associated with user.
 */
slots.create:aista.contacts.tickets

   // Making sure we've got a [meta] argument.
   if
      or
         not-exists:x:@.arguments/*/meta
         null:x:@.arguments/*/meta
         eq:x:@.arguments/*/meta
            .:
      .lambda
      
         // No [meta] argument provided
         return
            title:Tickets

   // Returning tickets associated with user.
   data.connect:[generic|tickets]
      data.read
         table:tickets
         columns
            id
            title
            description
            created
            status
         where
            and
               user.eq:x:@.arguments/*/meta
         order:created
         direction:desc

      // Iterating through each cloudlet returned above adding actions to each cloudlet.
      for-each:x:@data.read/*
         include:x:@.dp/#

            // Disabling close action if ticket is already closed.
            .disabled:bool:false
            if
               eq:x:@.dp/#/*/status
                  .:Closed
               .lambda
                  set-value:x:@.disabled
                     .:bool:true

            // Dynamically building our get messages URL.
            .get-messages
            set-value:x:@.get-messages
               strings.concat
                  .:magic/modules/tickets/admin/messages?order=created&direction=desc&limit=-1&ticket=
                  get-value:x:@.dp/#/*/id

            // Returning actions from [include].
            unwrap:x:+/**
            return
               click
                  title:Messages
                  url:x:@.get-messages
               actions
                  .
                     icon:check_circle
                     tooltip:Close the ticket
                     url:magic/modules/tickets/close-ticket
                     verb:put
                     disabled:x:@.disabled
                     payload
                        id:x:@.dp/#/*/id
                  .
                     icon:reply
                     tooltip:Reply to ticket
                     url:magic/modules/tickets/messages
                     verb:post
                     fields
                        .
                           key:description
                           type:textarea
                           props
                              placeholder:Description
                              required:bool:true
                              rows:int:5
                     payload
                        ticket:x:@.dp/#/*/id

         // Removing [id] to makes sure we don't return it to caller.
         remove-nodes:x:@.dp/#/*/id

      // Returning tickets to caller.
      add:x:./*/return/*/items
         get-nodes:x:@data.read/*
      return
         title:Tickets
         items
