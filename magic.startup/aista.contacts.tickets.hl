
/*
 * Plugin slot returning tickets associated with user.
 */
slots.create:aista.contacts.tickets

   // Making sure we've got a [meta] argument.
   if
      or
         not-exists:x:@.arguments/*/meta
         null:x:@.arguments/*/meta
         eq:x:@.arguments/*/meta
            .:
      .lambda
      
         // No [meta] argument provided
         return
            title:Tickets

   // Returning tickets associated with user.
   data.connect:[generic|tickets]
      data.read
         table:tickets
         columns
            title
            description
            created
            status
         where
            and
               user.eq:x:@.arguments/*/meta
         order:created
         direction:desc

      // Returning tickets to caller.
      add:x:./*/return/*/items
         get-nodes:x:@data.read/*
      return
         title:Tickets
         items
